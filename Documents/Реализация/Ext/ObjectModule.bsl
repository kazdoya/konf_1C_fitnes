
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПредварительнаяЗапись") Тогда
		Контрагент = ДанныеЗаполнения.Клиент;
		Комментарий = СтрШаблон("Введен на основании: %1 ", ДанныеЗаполнения.Ссылка);
		Сотрудник = ДанныеЗаполнения.Сотрудник;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		Для Каждого ТекСтрокаАбонементы Из ДанныеЗаполнения.Абонементы Цикл
			НоваяСтрока = Абонементы.Добавить();
			НоваяСтрока.Номенклатура = ТекСтрокаАбонементы.Абонемент;
			НоваяСтрока.КоличествоМесяцев = ТекСтрокаАбонементы.КоличествоМесяцев;
			НоваяСтрока.Стоимость = ТекСтрокаАбонементы.Стоимость;
		КонецЦикла;
		Для Каждого ТекСтрокаУслуги Из ДанныеЗаполнения.Услуги Цикл
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.Количество = ТекСтрокаУслуги.Количество;
			НоваяСтрока.Сумма = ТекСтрокаУслуги.Сумма;
			НоваяСтрока.Услуга = ТекСтрокаУслуги.Услуга;
			НоваяСтрока.Цена = ТекСтрокаУслуги.Цена;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма") + Абонементы.Итог("Стоимость");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ТоварыНаСкладах.Записать();
	Движения.УчётЗатрат.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	Движения.Хозрасчётный.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТовары.Номенклатура КАК Номенклатура,
	|	Реализация.Склад КАК Склад,
	|	СУММА(РеализацияТовары.Количество) КАК Количество,
	|	СУММА(РеализацияТовары.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Документ.Реализация КАК Реализация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Реализация.Товары КАК РеализацияТовары
	|		ПО Реализация.Ссылка = РеализацияТовары.Ссылка
	|ГДЕ
	|	РеализацияТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТовары.Номенклатура,
	|	Реализация.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияУслуги.Услуга,
	|	NULL,
	|	СУММА(РеализацияУслуги.Количество),
	|	СУММА(РеализацияУслуги.Сумма)
	|ИЗ
	|	Документ.Реализация.Услуги КАК РеализацияУслуги
	|ГДЕ
	|	РеализацияУслуги.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияУслуги.Услуга
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияАбонементы.Номенклатура,
	|	NULL,
	|	NULL,
	|	РеализацияАбонементы.Стоимость
	|ИЗ
	|	Документ.Реализация.Абонементы КАК РеализацияАбонементы
	|ГДЕ
	|	РеализацияАбонементы.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ВТ_Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|				ИЛИ ВТ_Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Абонемент)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоТовар,
	|	ВТ_Товары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ВТ_Товары.Количество КАК КоличествоВДокументе,
	|	ВТ_Товары.Сумма КАК СуммаВДокументе,
	|	ВТ_Товары.Склад КАК Склад,
	|	ТоварыНаСкладахОстатки.СрокГодности КАК СрокГодности,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ВТ_Товары.Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ВТ_Товары.Номенклатура.СчётБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				&МоментВремени,
	|				(Номенклатура, Склад) В
	|					(ВЫБРАТЬ
	|						ВТ_Товары.Номенклатура,
	|						ВТ_Товары.Склад
	|					ИЗ
	|						ВТ_Товары КАК ВТ_Товары)) КАК ТоварыНаСкладахОстатки
	|		ПО ВТ_Товары.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	|			И ВТ_Товары.Склад = ТоварыНаСкладахОстатки.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	СрокГодности
	|ИТОГИ
	|	МАКСИМУМ(КоличествоВДокументе),
	|	МАКСИМУМ(СуммаВДокументе),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.ЭтоТовар Тогда
			
			СтоимостьОбщая = 0;
			Превышение = ВыборкаНоменклатура.КоличествоВДокументе - ВыборкаНоменклатура.КоличествоОстаток;
			ОсталосьСписать = ВыборкаНоменклатура.КоличествоВДокументе;
			
			Если Превышение > 0 Тогда  
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре ""%1""  в количестве ""%2""", ВыборкаНоменклатура.НоменклатураПредставление, Превышение);
				Сообщение.Сообщить();
				Отказ = Истина;
			КонецЕсли;
			
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;	
			
			ВыборкаДетальные = ВыборкаНоменклатура.Выбрать();
			Пока ВыборкаДетальные.Следующий() И ОсталосьСписать > 0 Цикл
				Списываем = Мин(ВыборкаДетальные.КоличествоОстаток, ОсталосьСписать); 
				Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаДетальные.Номенклатура;
				Движение.Склад = Склад;
				Движение.СрокГодности = ВыборкаДетальные.СрокГодности;
				Движение.Количество = Списываем;
				Если Списываем = ВыборкаДетальные.КоличествоОстаток Тогда 
					СуммаСписания = ВыборкаДетальные.СуммаОстаток;
				Иначе
					СуммаСписания = Списываем / ВыборкаДетальные.КоличествоОстаток * ВыборкаДетальные.СуммаОстаток;
				КонецЕсли;
				Движение.Сумма = СуммаСписания; 
				ОсталосьСписать = ОсталосьСписать - Списываем; 
				СтоимостьОбщая = СтоимостьОбщая + Движение.Сумма;
			КонецЦикла;
			
			//регистр УчетЗатрат	
			Движение = Движения.УчётЗатрат.Добавить(); 
			Движение.Период = Дата;
			Движение.СтатьяЗатрат = ВыборкаНоменклатура.СтатьяЗатрат;
			Движение.Сумма = СтоимостьОбщая;
			/////////////
			
			//Проводка по отражению выручки Дт90 Кт40/10
			Движение = Движения.Хозрасчётный.Добавить();
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.Продажи;
			Движение.СчетКт = ВыборкаНоменклатура.СчетБухгалтерскогоУчета;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчётные.Номенклатура] = ВыборкаНоменклатура.Номенклатура; 
			Движение.Период    = Дата;
			Движение.Сумма = СуммаСписания;
			Движение.Содержание    = "Списана себестоимость товарно-материальных ценностей";
			
		КонецЕсли;
		
		//регистр Продажи
		Движение = Движения.Продажи.Добавить(); 
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
		Движение.Сотрудник = Сотрудник;
		Движение.Контрагент = Контрагент;
		Движение.Сумма = ВыборкаНоменклатура.СуммаВДокументе;
		Движение.Количество = ВыборкаНоменклатура.КоличествоВДокументе;
		/////////////
		
		
		
	КонецЦикла;
////////////////////////////////////////////////////////////////////////////////

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// регистр ЗаказыКлиентов
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Движения.ЗаказыКлиентов.Записывать = Истина;
		Движение = Движения.ЗаказыКлиентов.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Клиент = Контрагент;
		Движение.ЗаказКлиента = ДокументОснование;
		Движение.Сумма = Услуги.Итог("Сумма") + Абонементы.Итог("Стоимость");
	КонецЕсли;
	
	//Проводка по отражению выручки Дт62 Кт90
		Движение = Движения.Хозрасчётный.Добавить();
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.Продажи;
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, Контрагент);
		Движение.Период = Дата;
		Движение.Сумма = СуммаДокумента;
		Движение.Содержание = "Отражена выручка от реализации товаров и услуг";
	
	
КонецПроцедуры
