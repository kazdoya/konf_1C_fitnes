
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваровИУслуг") Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходаДенег.ОплатаПоставщику;
		Договор = ДанныеЗаполнения.Договор;
		Получатель = ДанныеЗаполнения.Контрагент;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Реализация") Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходаДенег.ВозвратПокупателю;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		Получатель = ДанныеЗаполнения.Контрагент;
		Договор = ДанныеЗаполнения.Договор;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	КонецЕсли;
	
	КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ДенежныеСредства Расход
	Движения.ДенежныеСредства.Записывать = Истина;
	Движение = Движения.ДенежныеСредства.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Касса = Касса;
	Движение.Сумма = СуммаДокумента;

	//регистр бухгалтерии Хозрасчетный
	Движения.Хозрасчётный.Записывать = Истина;
	АналитикаПроводки = ПолучитьАналитикуПроводки();
	
	Движение = Движения.Хозрасчётный.Добавить();
	Движение.СчетДт = АналитикаПроводки.СчетДебета;
	Движение.СчетКт = АналитикаПроводки.СчетКредита;
	Движение.Период = Дата;
	Движение.Сумма = СуммаДокумента;
	Движение.Содержание = АналитикаПроводки.СодержаниеОперации;
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, АналитикаПроводки.СубконтоДебет);
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт, Движение.СубконтоКт, АналитикаПроводки.СубконтоКредит);
	
КонецПроцедуры


Функция ПолучитьАналитикуПроводки()
	
	ОплатаПоставщику            = Перечисления.ВидыОперацийРасходаДенег.ОплатаПоставщику;
	ВозвратПокупателю     		= Перечисления.ВидыОперацийРасходаДенег.ВозвратПокупателю;
	ВыдачаЗаработнойПлаты     	= Перечисления.ВидыОперацийРасходаДенег.ВыдачаЗаработнойПлаты;
	//ВыдачаПодотчётнику     	    = Перечисления.ВидыОперацийРасходаДенег.ВыдачаПодотчётнику;
	
	СтруктураАналитики = Новый Структура;
	
	СтруктураАналитики.Вставить("СчетКредита", ПланыСчетов.Хозрасчетный.Касса);
	СтруктураАналитики.Вставить("СубконтоКредит", Касса);
	
	Если ВидОперации = ОплатаПоставщику Тогда
		СтруктураАналитики.Вставить("СчетДебета", ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками);
		СтруктураАналитики.Вставить("СодержаниеОперации", "Оплата поставщику наличными из кассы");
		СтруктураАналитики.Вставить("СубконтоДебет", Получатель);
	ИначеЕсли ВидОперации = ВозвратПокупателю Тогда 
		СтруктураАналитики.Вставить("СчетДебета", ПланыСчетов.Хозрасчетный.РасчетыСПокупателями);
		СтруктураАналитики.Вставить("СодержаниеОперации", "Возврат покупателю наличными из кассы"); 
		СтруктураАналитики.Вставить("СубконтоДебет", Получатель);
	ИначеЕсли ВидОперации = ВыдачаЗаработнойПлаты Тогда 
		СтруктураАналитики.Вставить("СчетДебета", ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
		СтруктураАналитики.Вставить("СодержаниеОперации", "Выплата заработной платы наличными из кассы"); 
		СтруктураАналитики.Вставить("СубконтоДебет", Получатель);	
	Иначе
		СтруктураАналитики.Вставить("СчетДебета", ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами);
		СтруктураАналитики.Вставить("СодержаниеОперации", "Выдача денежных средств подотчетному лицу"); 
		СтруктураАналитики.Вставить("СубконтоДебет", Получатель);
	КонецЕсли;
	
	Возврат СтруктураАналитики;
	
КонецФункции
